# Copyright 2017 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//v8binding/node.gni")

loadable_module("node_yue") {
  output_name = "yue"
  output_extension = "node"
  output_prefix_override = true  # do not add "lib" prefix

  sources = [
    "bindings.cc",
    "signal.cc",
    "signal.h",
  ]

  if (node_runtime != "electron") {
    sources += [
      "node_integration.cc",
      "node_integration.h",
      "node_integration_linux.cc",
      "node_integration_linux.h",
      "node_integration_mac.cc",
      "node_integration_mac.h",
      "node_integration_win.cc",
      "node_integration_win.h",
    ]
  }

  deps = [
    "//base",
    "//nativeui",
    "//v8binding",
  ]

  if (is_linux && is_component_build) {
    configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}
