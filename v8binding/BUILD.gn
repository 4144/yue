# Copyright 2017 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

declare_args() {
  # The version of Node to build against to.
  node_version = "v7.7.1"
}

config("node_config") {
  defines = [
    "USING_UV_SHARED=1",
    "USING_V8_SHARED=1",
    "BUILDING_NODE_EXTENSION",
  ]

  node_dir = "//third_party/node-" + node_version

  include_dirs = [
    node_dir + "/include/node",
    node_dir + "/src",
    node_dir + "/deps",
    node_dir + "/deps/uv/include",
    node_dir + "/deps/v8/include",
    node_dir + "/deps/chakrashim/include",
  ]

  if (is_mac) {
    ldflags = [
      "-undefined", "dynamic_lookup",
    ]
  }
}

source_set("v8binding") {
  sources = [
    "arguments.cc",
    "arguments.h",
    "callback.cc",
    "callback.h",
    "callback_internal.h",
    "dict.h",
    "per_isolate_data.cc",
    "per_isolate_data.h",
    "prototype.cc",
    "prototype.h",
    "prototype_internal.h",
    "template_util.h",
    "types.h",
    "v8binding.h",
  ]

  public_configs = [ ":node_config" ]
}

