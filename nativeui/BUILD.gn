# Copyright 2016 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//testing/test.gni")

declare_args() {
  # Whether to build for GTK+3.
  use_gtk3 = false
}

source_set("nativeui") {
  sources = [
    "container.cc",
    "container.h",
    "init.h",
    "types.h",
    "view.h",
    "label.h",
    "window.cc",
    "window.h",
    "graphics/font.h",
    "graphics/color.h",
    "layout/box_layout.cc",
    "layout/box_layout.h",
    "layout/fill_layout.cc",
    "layout/fill_layout.h",
    "layout/layout_manager.h",
    "gtk/gtk_event_loop.cc",
    "gtk/gtk_event_loop.h",
    "gtk/container_gtk.cc",
    "gtk/init_gtk.cc",
    "gtk/label_gtk.cc",
    "gtk/view_gtk.cc",
    "gtk/window_gtk.cc",
    "mac/container_mac.mm",
    "mac/init_mac.mm",
    "mac/label_mac.mm",
    "mac/view_mac.mm",
    "mac/window_mac.mm",
    "win/base_view.cc",
    "win/base_view.h",
    "win/gdiplus.h",
    "win/subwin_holder.cc",
    "win/subwin_holder.h",
    "win/subwin_view.cc",
    "win/subwin_view.h",
    "win/window_impl.cc",
    "win/window_impl.h",
    "win/color_win.cc",
    "win/font_win.cc",
    "win/container_win.cc",
    "win/init_win.cc",
    "win/label_win.cc",
    "win/view_win.cc",
    "win/screen.cc",
    "win/screen.h",
    "win/window_win.cc",
  ]

  deps = [
    "//base",
    "//ui/gfx",
  ]

  if (is_linux) {
    if (use_gtk3) {
      public_configs = [ "//build/config/linux/gtk3" ]
    } else {
      public_configs = [ "//build/config/linux/gtk2" ]
    }
  } else if (is_win) {
    libs = [
      "gdi32.lib",
      "gdiplus.lib",
    ]
    configs -= [ "//build/config/win:lean_and_mean" ]
  }
}

test("nativeui_unittests") {
  sources = [
    "container_unittest.cc",
    "label_unittest.cc",
    "view_unittest.cc",
    "window_unittest.cc",
    "layout/fill_layout_unittest.cc",
    "layout/box_layout_unittest.cc",
    "test/run_all_unittests.cc",
  ]

  deps = [
    ":nativeui",
    "//base/test:test_support",
    "//testing/gtest",
    "//ui/gfx:test_support",
  ]
}
