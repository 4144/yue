# Copyright 2016 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//testing/test.gni")

# Independent lua executable with yue runtime.
executable("yue") {
  sources = [ "yue.cc" ]
  deps = [ ":yue_runtime" ]
}

# Loadable module of core yue libraries to be used with other lua distributions,
# currently we only build bindings to GUI part of yue.
loadable_module("lua_yue") {
  output_name = "yue"
  output_prefix_override = true  # do not add "lib" prefix

  deps = [ ":lua_yue_gui" ]

  if (is_mac) {
    ldflags = [
      "-undefined", "dynamic_lookup",
    ]
  } else if (is_linux && is_component_build) {
    configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
  }
}

# Component used for constructing a lua environment with yue inside.
source_set("yue_runtime") {
  sources = [
    "api_sys.cc",
    "api_sys.h",
    "api_util.cc",
    "api_util.h",
    "builtin_loader.cc",
    "builtin_loader.h",
  ]

  deps = [
    ":lua_yue_gui",
    "//third_party/lua:liblua",
  ]
}

# Lua bindings of yue's GUI part.
source_set("lua_yue_gui") {
  sources = [
    "api_gui.cc",
    "api_gui.h",
    "api_signal.h",
  ]

  deps = [
    "//base",
    "//lua",
    "//nativeui",
  ]
}

test("yue_unittests") {
  sources = [
    "api_signal_unittest.cc",
    "test/run_all_unittests.cc",
  ]

  deps = [
    ":yue_runtime",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}
